apply plugin: 'com.android.library'
apply plugin: 'signing'
apply plugin: 'maven'

group='com.github.mbiamont'
version='1.2.0'

android {
    compileSdkVersion 24
    buildToolsVersion '23.0.3'

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'org.aspectj:aspectjrt:1.8.5'
    compile 'com.google.android.gms:play-services-analytics:9.0.2'
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidJavadocsJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {

        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: CENTRAL_USERNAME, password: CENTRAL_PASSWORD)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: CENTRAL_USERNAME, password: CENTRAL_PASSWORD)
            }

            pom.project {
                name 'Google Analytics Annotations Gradle Plugin'
                packaging 'jar'
                description 'Gradle plugin to use Google Analytics Annotations Library'
                url 'https://github.com/mbiamont/google-analytics-annotations'

                scm {
                    connection 'scm:git:https://github.com/mbiamont/google-analytics-annotations.git'
                    developerConnection 'scm:git:https://github.com/mbiamont/google-analytics-annotations.git'
                    url 'https://github.com/mbiamont/google-analytics-annotations'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'mbiamont'
                        name 'Melvin Biamont'
                        email 'melvin.biamont@gmail.com'
                    }
                }
            }
        }
    }
}